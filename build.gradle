plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.2'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	//mavenLocal()
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com" }
	/*maven {
		name = "Enchantment Descriptions"
		url = 'https://maven.blamejared.com'
	}*/
	maven {
		name = "Patchouli" // https://github.com/Vazkii/Patchouli
		url = 'https://maven.blamejared.com'
	}
	maven {
		name = "Ladysnake Mods"
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
	maven {
		name = "entity reach attribute"
		url = "https://maven.jamieswhiteshirt.com/libs-release/"
	}
	maven { url 'https://maven.wispforest.io/' } // owo lib
	maven {
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://maven.nucleoid.xyz/" }
	maven {
		name = "Curse"
		url = 'https://cfa2.cursemaven.com'
	}
	mavenCentral()
}

dependencies {
	// jetbrains annotations
	implementation 'org.jetbrains:annotations:23.1.0'

	implementation include('org.jgrapht:jgrapht-core:1.5.1')

	// fabric & api
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// mine
	// TODO - Switch to official builds once PR's are merged
	//modImplementation "com.github.DaFuqs:Revelationary:${project.revelationary_version}"
	modImplementation "com.github.Noaaan:Revelationary:${project.revelationary_fork}"
	modImplementation include("com.github.DaFuqs:AdditionalEntityAttributes:${project.additional_entity_attributes_version}")
	//modImplementation include("com.github.DaFuqs:Arrowhead:${project.arrowhead_version}")
	modImplementation include("com.github.Noaaan:Arrowhead:${project.arrowhead_fork}")
	//modImplementation include("com.github.DaFuqs:DimensionalReverb:${project.dimensional_reverb_version}")
	modImplementation include("com.github.Noaaan:DimensionalReverb:${project.dimensional_reverb_fork}")

	// we need a bit of concern
	//modImplementation include("com.github.devs-immortal:Incubus-Core:${project.incubus_core_version}")
	modImplementation include("com.github.Noaaan:Incubus-Concern:${project.incubus_core_fork}")

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("com.terraformersmc:modmenu:${project.modmenu_version}")

	modImplementation "eu.pb4:common-protection-api:${project.cpa_version}"

	//modImplementation "vazkii.patchouli:Patchouli:${project.patchouli_version}"
	// https://maven.blamejared.com/vazkii/patchouli/Patchouli/
	modImplementation "io.wispforest:owo-lib:${project.owo_version}"

	// https://github.com/shedaniel/RoughlyEnoughItems
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modApi "dev.architectury:architectury-fabric:${project.architectury_version}"

	// https://github.com/emilyploszaj/emi
	modCompileOnly "dev.emi:emi:${emi_version}:api"

	switch (recipe_viewer.toLowerCase(Locale.ROOT)) {
		case "rei": modLocalRuntime("me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"); break
		case "emi": modLocalRuntime("dev.emi:emi:$emi_version"); break
		case "disabled": break
		default: println("Unknown recipe viewer specified: $recipe_viewer. Must be EMI, REI or disabled.")
	}

	// https://github.com/emilyploszaj/trinkets
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"

	// https://github.com/OnyxStudios/Cardinal-Components-API/wiki/
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}")
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}")
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-item:${project.cca_version}")
	modImplementation include("dev.onyxstudios.cardinal-components-api:cardinal-components-level:${project.cca_version}")

	modImplementation include("com.github.emilyploszaj:step-height-entity-attribute:${project.step_height_attribute_version}")
	modImplementation include("com.jamieswhiteshirt:reach-entity-attributes:${project.reach_entity_attributes_version}")

	implementation include("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
	annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")

	// https://github.com/Terrails/HealthOverlay
	// Thanks to yurisuika's Raised for the head-up (https://github.com/yurisuika/Raised/blob/Fabric-1.19/build.gradle)
	modCompileOnly ("curse.maven:health-overlay-fabric-313219:${project.health_overlay_version}") {transitive = false}

	// Soft testing-only dependencies:
	//modRuntimeOnly 'com.github.DaFuqs:PaginatedAdvancements:1.19-SNAPSHOT'
	// Descriptions for all Enchantments https://maven.blamejared.com/net/darkhax/enchdesc/
	//modCompileOnly("net.darkhax.enchdesc:EnchantmentDescriptions-Common-${project.enchantment_descriptions_mc_version}:${project.enchantment_descriptions_mod_version}")
	//modRuntimeOnly("squeek.appleskin:appleskin-fabric:mc1.18.2-2.4.1") { exclude module: 'modmenu' }
}

loom {
	accessWidenerPath = file("src/main/resources/spectrum.accesswidener")

	runs {
		datagen {
			server()

			name "Minecraft Data"
			vmArg "-Dfabric-api.datagen"
			vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"

			runDir "build/datagen"
		}
	}
}

sourceSets {
	main {
		resources {
			srcDirs += [
					'src/main/generated'
			]
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 17 // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE.md") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		mavenLocal()
	}
}
