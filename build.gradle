plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {

	mavenLocal()
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com" }
	maven {
		name = "Enchantment Descriptions"
		url = 'https://maven.blamejared.com'
	}
	maven {
		name = "Patchouli" // https://github.com/Vazkii/Patchouli
		url = 'https://maven.blamejared.com'
	}
	maven {
		name = "Ladysnake Mods"
		url = 'https://ladysnake.jfrog.io/artifactory/mods'
	}
	maven {
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = "banner++"
		url "https://maven.theepicblock.nl"
	}
	maven {
		name "entity reach attribute"
		url "https://maven.jamieswhiteshirt.com/libs-release/"
	}
	/*maven {
		name "bannerpp"
		url 'https://server.bbkr.space/artifactory/libs-release/'
	}*/
	maven { url 'https://maven.wispforest.io/' } // owo lib
	maven {
		name = "JitPack"
		url = 'https://jitpack.io'
		//metadataSources {// thank you, Incubus Core >:(
		//	artifact() //Look directly for artifact
		//}
	}
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	//mavenCentral() // incubus core

	maven {
		name = "ProgrammerArtInjector"
		url = 'https://maven.extracraftx.com'
	}

	maven {
		name = "Shimmer"
		url = 'https://maven.firstdarkdev.xyz/snapshots'
	}

	maven {
		url 'https://cfa2.cursemaven.com'
	}
}

dependencies {
	// jetbrains annotations @Nullabe @NotNull
	implementation 'org.jetbrains:annotations:23.0.0'

	// fabric
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// dependencies
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.github.DaFuqs:Revelationary:${project.revelationary_version}"

	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"){
		exclude(group: "net.fabricmc.fabric-api")
	}
	modApi("com.terraformersmc:modmenu:${project.modmenu_version}")

	modImplementation "vazkii.patchouli:Patchouli:${project.patchouli_version}"	// https://maven.blamejared.com/vazkii/patchouli/Patchouli/
	modImplementation "io.wispforest:owo-lib:${project.owo_version}+1.18"

	// https://github.com/shedaniel/RoughlyEnoughItems
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

	// https://github.com/emilyploszaj/trinkets
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"

	// https://github.com/OnyxStudios/Cardinal-Components-API/wiki/
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-item:${cca_version}"

	// https://github.com/TheEpicBlock/banner-plus-plus
	modImplementation include("io.github.fablabsmc:bannerpp:2.0.6-teb.1+mc.1.18.2")
	modImplementation include("com.github.emilyploszaj:step-height-entity-attribute:${project.step_height_attribute_version}")
	modImplementation include("com.github.DaFuqs:AdditionalEntityAttributes:${project.additional_entity_attributes_version}")

	modImplementation include("com.jamieswhiteshirt:reach-entity-attributes:2.1.1")
	modImplementation include("azzy.fabric:incubus-core:${project.incubus_core_version}")
	modImplementation include("com.github.devs-immortal:Some-Assembly-Required:${project.sar_version}")
	modImplementation include("com.github.devs-immortal:Divine-Intervention:${project.divine_intervention_version}")
	modImplementation include("io.github.ladysnake:satin:${project.satin_version}")
	modImplementation("com.github.LudoCrypt:Liminal-Library:5.2.1")

	// Soft dependencies:
	// Descriptions for all Enchantments https://maven.blamejared.com/net/darkhax/enchdesc/
	modCompileOnly("net.darkhax.enchdesc:EnchantmentDescriptions-Common-${project.enchantment_descriptions_mc_version}:${project.enchantment_descriptions_mod_version}")

	// includes the textures in the "programmer art" folder in the programmer art texture pack
	modImplementation "com.extracraftx.minecraft:ProgrammerArtInjector:${project.programmer_art_injector_version}"
	include "com.extracraftx.minecraft:ProgrammerArtInjector:${project.programmer_art_injector_version}"

	// colored lights: purely soft dependencies
	// https://github.com/Gegy/colored-lights // uses colored_lights/light_colors.json
	modImplementation "com.lowdragmc.shimmer:Shimmer-fabric-1.18.2:0.1.9"
}

loom {
	accessWidenerPath = file("src/main/resources/spectrum.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 17 // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE.md") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
		mavenLocal()
	}
}
